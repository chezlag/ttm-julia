{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1. Algoritmos\n",
    "\n",
    "Temas:\n",
    "- Aproximación a $e$\n",
    "- Shooting algorithm\n",
    "\n",
    "### Introducción\n",
    "\n",
    "Un algoritmo es una serie de instrucciones que permiten resolver un problema. \n",
    "\n",
    "- Conjunto finito de pasos\n",
    "- Instrucciones concretas y bien definidas\n",
    "- Con un punto de partida y un punto de llegada\n",
    "\n",
    "### Aproximación a $e$\n",
    "\n",
    "El número $e$ puede expresarse como\n",
    "\n",
    "$$\n",
    "e = \\sum_{n = 0}^\\infty \\frac{1}{n!}\n",
    "$$\n",
    "\n",
    "Dado que no podemos sumar infinitos términos, podemos aproximarnos al número $e$ con la suma de los primeros $N$ términos.\n",
    "\n",
    "El algoritmo para aproximarnos a $e$ sería\n",
    "\n",
    "1. **Inicialización**:\n",
    "   - Establecer la tolerancia `tol` (el criterio para parar de sumar, cuando la aproximación está \"suficientemente cerca\" del valor real de $e$) \n",
    "   - Establecer el número máximo de términos a sumar `N`\n",
    "   - Inicializar la suma `e` a 1 (corresponde al primer término de la serie).\n",
    "   - Inicializar el valor del factorial `fct` a 1 (inicialmente representa 0!).\n",
    "\n",
    "2. **Iteración**:\n",
    "   - Para $n$ desde 1 hasta `N`, hacer lo siguiente:\n",
    "     - Calcular el factorial de $n$ acumulando en `fct` (`fct = fct * n`).\n",
    "     - Actualizar el valor de `e` sumando $\\frac{1}{fct} $ al valor actual de `e`.\n",
    "     - Mostrar los valores actuales de `fct` y `e` para depuración.\n",
    "     - Comparar la diferencia absoluta entre `e` y el valor real de $e$ (`exp(1)`):\n",
    "       - Si la diferencia es menor o igual a `tol`, entonces:\n",
    "         - Terminar el bucle (`break`).\n",
    "\n",
    "3. **Resultado**:\n",
    "   - El valor de `e` ahora contiene la aproximación de $e$ con la precisión especificada.\n",
    "\n",
    "Este algoritmo se puede utilizar para aproximar el valor de $e$ con un error menor o igual a la tolerancia especificada, siempre que el número de iteraciones sea suficiente."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Definimos parámetros para el problema\n",
    "tol = 0.001 # tolerancia (cuando es que estamos \"suficientemente cerca\")\n",
    "N = 10000 # máximo número de iteraciones\n",
    "\n",
    "# Valores iniciales\n",
    "e = 1\n",
    "fct = 1\n",
    "\n",
    "for n = 1:N\n",
    "\tfct *= n # calculo el factorial en cada iteración\n",
    "    e += 1 / fct # redefino e con cada loop\n",
    "\t@show n, fct, e\n",
    "    if (abs(e - exp(1)) <= tol) # comparo con exp(1), el valor real\n",
    "        break # si la aproximación está más cerca de e que tol, entonces corto el loop\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Para reiterar y aclarar que hicimos:\n",
    "\n",
    "- `N` define el número máximo de iteraciones (para que el programa no corra infinitamente)\n",
    "- `e` es nuestra aproximación de $e$, actualizado en cada iteración.\n",
    "\t- `e` empieza en 1 porque $0! = 1$ y la sumatoria empieza en $n=0$\n",
    "- `fct` corresponde a `n!`, calculado manualmente en cada iteración. \n",
    "\t- También podríamos haber utilizado la función `factorial(n)`\n",
    "- `@show` es un macro, equivalente en este caso a: `println(\"(fct, e) = ($fct, $e)\")\n",
    "\n",
    "Podemos resolver el mismo problema con una función, permitiéndonos cambiar los parámetros de tolerancia y máximas iteraciones, sin reescribir el loop constantemente."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function approx_e(tol; N = 10000)\n",
    "\te = 0.0\n",
    "\tfor n = 0:N\n",
    "\t    e += 1 / factorial(n)\n",
    "\t\t@show n, e\n",
    "\t    if (abs(e - exp(1)) <= tol)\n",
    "\t        break\n",
    "\t    end\n",
    "\tend\n",
    "\treturn e\n",
    "end\n",
    "\n",
    "approx_e(0.0000000001)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Shooting algorithm\n",
    "\n",
    "#### El modelo\n",
    "\n",
    "Partamos de un modelo de crecimiento neoclásico en horizonte finito:\n",
    "\n",
    "$$\n",
    "\\max_{\\{c_{t}, k_{t+1}\\}_{t=0}^{T}} \\sum_{t=0}^{T}\\beta^{t}u(c_{t})\n",
    "$$\n",
    "\n",
    "Sujeto a las siguientes restricciones:\n",
    "\n",
    "- Restricción de recursos: $\\quad c_{t}+k_{t+1} \\leq f(k_{t})\\equiv F(k_{t}) + (1-\\delta)k_{t}$\n",
    "- Restricción de no-negatividad: $\\quad c_t \\geq 0, \\quad k_{t+1} \\geq 0$\n",
    "- Condición inicial: $\\quad k_0 > 0$ dado\n",
    "\n",
    "La ecuación de Euler del modelo viene dada por:\n",
    "\n",
    "$$\\text{(EE):}\\quad u'(c_t) = \\beta u'(c_{t+1}) f'(k_{t+1})$$\n",
    "\n",
    "#### El algoritmo\n",
    "\n",
    "Si no hay depreciación completa del capital ($\\delta < 1$), entonces no hay solución en forma cerrada. Para hallar una solución, usamos un algoritmo computacional.\n",
    "\n",
    "El **shooting algorithm** involucra los siguientes pasos:\n",
    "\n",
    "1. Adivinar un valor inicial para $c_0$\n",
    "2. Computar la secuencia que cumpla la Ecuación de Euler $\\forall t$ dado $(k_0, c_0)$.\n",
    "3. Testear si la secuencia es la solución, chequeando que se cumple la condición terminal sobre $k_{T+1}$\n",
    "\t- Si $k_{T+1} \\approx 0$ entonces:\n",
    "\t\t- La secuencia que empieza con $(k_{0}, c_{0})$ es la solución\n",
    "\t- Si no, elegir un nuevo valor de $c_0$ y repetir pasos 1-3\n",
    "\n",
    "Dos problemas a resolver en la implementación:\n",
    "\n",
    "- ¿Cómo elegir el valor inicial?  Usar solución para $\\delta = 1$\n",
    "- ¿Cómo actualizar el valor de $c_0$? Algoritmos de búsqueda (ej: bisection search)\n",
    "\n",
    "#### La implementación\n",
    "\n",
    "Para implementar el algoritmo tenemos que suponer una forma para la función de utilidad y la función de producción, el valor de los principales parámetros del problema, y un horizonte temporal para resolver. \n",
    "\n",
    "En este caso, vamos a suponer que la función de utilidad es logarítimica y que la función de producción es una Cobb-Douglas.\n",
    "\n",
    "$$\n",
    "u(c) = \\log(c); \\quad F(k) = Ak^\\alpha\n",
    "$$\n",
    "\n",
    "Dado que $u'(c)>0$ la restricción de recursos siempre va a estar activa:\n",
    "\n",
    "$$\n",
    "c_t + k_{t+1} = \\underbrace{F(k_t) - (1-\\delta)k_t}_{f(k_t)}\n",
    "$$\n",
    "\n",
    "y podemos expresar la EE como\n",
    "\n",
    "$$\n",
    "\\frac{1}{f(k_t) - k_{t+1}} = \\beta \\cdot \\frac{1}{f(k_{t+1}) + k_{t+2}} \\cdot f'(k_{t+1})\n",
    "$$\n",
    "\n",
    "Comenzamos por inicializar los parámetros del modelo. Supondremos que $k_0 = 10, \\beta = 0.95, \\delta = 0.1, A = 1, \\alpha = 0.3$. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "k0 = 10.0\n",
    "β = 0.95\n",
    "δ = 0.1\n",
    "α = 0.3\n",
    "\n",
    "A = 1.0"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Luego, tenemos que encontrar una forma de despejar la trayectoria del capital y el consumo para que la secuencia cumpla la EE en cada punto. \n",
    "\n",
    "Despejando de la EE tenemos que \n",
    "$$c_{t+1} = u'^{-1}\\left(\\frac{u'(c_{t})}{\\beta \\cdot f'(k_{t+1})}\\right)$$\n",
    "\n",
    "donde\n",
    "$$\\begin{array}{rl}\n",
    "f(k) &= k^\\alpha + (1-\\delta)k \\\\ \n",
    "f'(k)&= \\alpha k^{\\alpha -1} + 1 - \\delta \\\\\n",
    "u(c) &= log(c) \\\\\n",
    "u'(c)&= 1/c \\\\\n",
    "u'^{-1}(c) &= 1/c\n",
    "\\end{array}$$\n",
    "\n",
    "Definimos las funciones principales del problema:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"Utility function\"\n",
    "u(c) = log(c)\n",
    "\n",
    "\"Derivative of utility\"\n",
    "u_prime(c) = 1/c\n",
    "\n",
    "\"Inverse of derivative of utility\"\n",
    "u_prime_inv(c) = 1/c\n",
    "\n",
    "\"Production function\"\n",
    "function F(k, A = A, α = α)\n",
    "    if k > 0\n",
    "        return A * k^α\n",
    "    else\n",
    "        return 0\n",
    "    end\n",
    "end\n",
    "\n",
    "\"Derivative of production function\"\n",
    "function F_prime(k, A = A, α = α) \n",
    "    if k < 0\n",
    "        return 0\n",
    "    else\n",
    "        return A * α * k^(α-1)\n",
    "    end\n",
    "end\n",
    "\n",
    "\"Production function (w depreciation)\"\n",
    "function f(k, A = A, α = α, δ = δ)\n",
    "    if k > 0\n",
    "        return F(k) + (1 - δ) * k\n",
    "    else\n",
    "        return 0\n",
    "    end\n",
    "end\n",
    "\n",
    "\"Derivative of production function (w depreciation)\"\n",
    "function f_prime(k, A = A, α = α, δ = δ) \n",
    "    if k < 0\n",
    "        return 0\n",
    "    else\n",
    "        return F_prime(k) + (1 - δ)\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Creamos una función que devuelva $(k_{t+1}, c_{t+1})$ en base a $(k_t, c_t)$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"Compute next capital and consumption based on current values\"\n",
    "function next_k_c(k, c, A = A, α = α, δ = δ, β = β)\n",
    "    k_next = f(k) - c\n",
    "    c_next = u_prime_inv(u_prime(c) / (β * (f_prime(k_next))))\n",
    "    return k_next, c_next\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Por último, creamos una función `shooting` que nos devuelve toda la secuencia de consumo y capital, en base a los valores inciales de $(k_0, c_0)$, siguiendo la regla establecida en `next_k_c`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"Shooting algorithm\"\n",
    "function shooting(k0, c0, T, A = A, α = α, δ = δ, β = β; verbose = false)\n",
    "    # initialize vectors \n",
    "    k_vec = zeros(T+2)\n",
    "    c_vec = zeros(T+1)\n",
    "    \n",
    "    k_vec[1] = k0\n",
    "    c_vec[1] = c0\n",
    "\n",
    "    for t in 1:T\n",
    "        k_vec[t+1], c_vec[t+1] = next_k_c(k_vec[t], c_vec[t], A, α, δ, β)\n",
    "        if verbose\n",
    "            display(\"$t : k = $(k_vec[t+1]), c = $(c_vec[t+1])\")\n",
    "        end\n",
    "    end\n",
    "\n",
    "    k_vec[T+2] = f(k_vec[T+1]) + (1 - δ) * k_vec[T+1] - c_vec[T+1]\n",
    "\n",
    "    return [k_vec, c_vec]\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Implementamos un algoritmo de búsqueda por bisección:\n",
    "\n",
    "1. Determinamos rango para buscar $c_0 = (c_{lo}, c_{hi})$ \n",
    "\t- Tomamos dos valores extremos: 0 (maximo ahorro) y $f(k_0)$ (máximo consumo)\n",
    "2. Testear el punto medio del intervalo de búsqueda como posible $c_0$\n",
    "3. Computar secuencias de capital y consumo que cumplan con la EE en cada período\n",
    "4. Chequear si $k_{T+1} = 0$\n",
    "\t- Si $k_{T+1} \\approx 0$, el valor probado para $c_0$ es óptimo.\n",
    "\t- Si $k_{T+1} > 0$ hay demasiado ahorro. Elegir $c_0$ más alto (descartar valores por debajo de $c_0$).\n",
    "\t- Si $k_{T+1} < 0$ hay demasiado consumo. Elegir $c_0$ más bajo (descartar valores por encima de $c_0$).\n",
    "5. Repetir pasos 2–4 hasta hallar el óptimo."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "k0 = 10.0\n",
    "\n",
    "c0_lo = 0.0   # mínimo: no hay consumo\n",
    "c0_hi = f(k0) # máximo: no hay ahorro\n",
    "\n",
    "c0 = (c0_lo + c0_hi) / 2 # guess inicial: punto medio\n",
    "\n",
    "maxiter = 10e5           # evitar loop infinito\n",
    "tol = 10e-13             # tolerancia\n",
    "\n",
    "i = 0\n",
    "while i < maxiter\n",
    "\tc0 = (c0_lo + c0_hi) / 2     # actualizar guess\n",
    "\tk,c = shooting(k0, c0, 100)  # calculo vectores de k y c en base a guess\n",
    "\n",
    "    if k[end] ≈ 0 || (c0_hi - c0_lo) / 2 < tol\n",
    "        display(\"Solución: c0 = $(c0)\")\n",
    "        break\n",
    "    elseif k[end] > 0\n",
    "        c0_lo = c0\n",
    "    else\n",
    "        c0_hi = c0\n",
    "    end\n",
    "\n",
    "\ti += 1\n",
    "end\n",
    "c0_optim = c0;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Por último, graficamos las trayectorias óptimas de capital y consumo:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using LaTeXStrings, Plots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# plot optimal trajectories of capital and consumption\n",
    "k_vec, c_vec = shooting(k0, c0_optim, 100);\n",
    "plotk = plot(k_vec, title = \"Capital\", yaxis = L\"k_t\")\n",
    "plotc = plot(c_vec, title = \"Consumption\", yaxis = L\"c_t\",  color = \"red\")\n",
    "plot(plotk, plotc, layout = (1, 2), legend = false, xaxis = \"t\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
